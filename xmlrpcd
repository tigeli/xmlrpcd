#!/usr/bin/perl

use warnings;
use strict;

use IO::Socket::SSL;
use HTTP::Daemon::SSL;
use HTTP::Status;
use MIME::Base64;

use Frontier::RPC2;

use YAML qw/LoadFile/;

use App::Daemon qw/daemonize/;
use Log::Log4perl;

#-----------------------------------------------------------------------------
# load configuration

$App::Daemon::pidfile = 'run/xmlrpcd.pid';
$App::Daemon::l4p_conf = 'logging.conf';

my $options = {};

my $opts_file = App::Daemon::find_option("--config" => 1);
if (defined $opts_file) {
  $options = LoadFile($opts_file);
}

#-----------------------------------------------------------------------------
# prepare RPC mapping

my $rpc_hash = {
  'sample.sumAndDifference' => sub {
    my ($a, $b) = @_;
    return [$a + $b, $a - $b];
  },
};

#-----------------------------------------------------------------------------

daemonize();

my $logger = Log::Log4perl::get_logger("http");

my $daemon = new HTTP::Daemon::SSL(
  SSL_cert_file => $options->{"ssl_cert_file"},
  SSL_key_file  => $options->{"ssl_key_file"},
  LocalPort => $options->{"listen_port"},
  LocalAddr => $options->{"listen_addr"},
  Listen => 1,
  ReuseAddr => 1,
) or $logger->logdie("Socket listening error: ", $!);

my $rpc_server = new Frontier::RPC2();

CLIENT:
while (1) {
  my $client = $daemon->accept or next CLIENT;

  REQUEST:
  while (my $req = $client->get_request) {
    $logger->debug("Another request");

    if ($req->method ne 'POST') {
      $logger->info("HTTP request ", $req->method, ": denying");
      $client->send_error(RC_FORBIDDEN);
      next REQUEST;
    }

    # XXX: authenticate
    my $auth_user = authenticate($req);
    if (not defined $auth_user) {
      $logger->debug("Unauthorized request");
      my $resp = new HTTP::Response(401, 'Unauthorized');
      $resp->header('WWW-Authenticate' => 'Basic realm=""');

      $client->send_response($resp);
      next REQUEST;
    }

    $logger->debug("Request authorized (user ", $auth_user, ")");

    my $xml_resp = $rpc_server->serve(
      $req->content,
      $rpc_hash,
    );

    my $resp = new HTTP::Response(200, 'OK', undef, $xml_resp);

    $client->send_response($resp);
  }
}

#-----------------------------------------------------------------------------

sub authenticate {
  my ($req) = @_;

  my $auth_data = $req->header('Authorization');
  if (not defined $auth_data or
      $auth_data !~ s/^Basic (\S+).*/$1/i) {
    return undef;
  }

  my $logger = Log::Log4perl::get_logger("auth");

  # Authorization: Basic QWxhZGRpbjpvcGVuIHNlc2FtZQ==
  # username:password
  my ($user, $pass) = split /:/, decode_base64($auth_data), 2;

  $logger->debug("user: $user | pass: $pass");

  return $user;
}

#-----------------------------------------------------------------------------
# vim:ft=perl
